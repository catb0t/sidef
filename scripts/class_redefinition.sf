#!/usr/bin/ruby

# Class (re)definitions and inheritance

class Circle{
    method name {
        "Replace me!";
    };
};
class Square(side) {
    method diagonal {
        Math.sqrt(self.side**2 * 2);
    };
};
class Triangle{
    method area {
        self.base * self.height / 2;
    };
};

class Shape {
    method triangle(base, height) {
        Triangle(base, height);
    };
    method square(side) {
        Square(side);
    };
    method circle(radius) {
        Circle(radius);
    };
    method type {
        self.__name__;
    };
}

# Extend Triangle class
Triangle < class(base, height) < Shape {
    method area {
        die "Triangle inheritance error!";
    }
}

# Extend Square class ('<' is synonym with '<<')
Square << class() < Shape {
    method area {
        self.side ** 2;
    };
}

# Replace Circle class
Circle = class(radius) < Shape {
    method area {
        self.radius**2 * Math.pi;
    };
    method diameter {
        self.radius * 2;
    };
    method circumference {
        self.diameter * Math.pi;
    };
    method name {
        "Ringie";
    };
}

var shape = Shape();

var triangle = shape.triangle(base: 8, height: 5);
var circle = shape.circle(radius: 6);
var square = shape.square(side: 4);

say "Area of triangle with base #{triangle.base} and height #{triangle.height} is: #{triangle.area}";
say "Area of a circle with radius #{circle.radius} is: #{circle.area}";
say "Circumfere of a circle with radius #{circle.radius} is #{circle.circumference}";
say "Area of a square with side #{square.side} is #{square.area}";
say "Diagonal of a square with side #{square.side} is #{square.diagonal}";

circle.type   == "Circle"   || die "circle type error";
square.type   == "Square"   || die "square type error";
triangle.type == "Triangle" || die "triangle type error";
circle.name   == "Ringie"   || die "circle name error";
