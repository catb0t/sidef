#!/usr/bin/ruby

#
## http://rosettacode.org/wiki/Create_an_HTML_table#Sidef
#

class HTML {
    method _attr(h is Hash) {
        h.keys.sort.map {|k| %Q' #{k}="#{h[k]}"' }.join('')
    };

    method _tag(h is Hash, name, value) {
        "<#{name}" + self._attr(h) + '>' + value + "</#{name}>"
    };

    method table(h is Hash, *data) { self._tag(h, 'table', data.join('')) };
    method table(*data)            { self.table(:(), data...) };
}

class Table < HTML {
    method th(h is Hash, value) { self._tag(h, 'th', value) };
    method th(value)            { self.th(:(), value) };

    method tr(h is Hash, *rows) { self._tag(h, 'tr', rows.join('')) };
    method tr(*rows)            { self.tr(:(), rows...) };

    method td(h is Hash, value) { self._tag(h, 'td', value) };
    method td(value)            { self.td(:(), value) };
}

var header = %w(&nbsp; X Y Z);

var data = 30..45;
var rows = 5;

var html = HTML.new;
var table = Table.new;

var code = html.table(
    :(  # attributes
        cellspacing => 4,
        style => "text-align:right; border: 1px solid;"
     ),

    # header
    table.tr(header.map{|elem| table.th(elem)}...),

    # rows
    (1..rows).map { |i|
        table.tr(
            table.td(:(align => 'right'), i),
            (header.len - 1).of {
                table.td(:(align => 'right'), data.shift)
            }...
        )
    }...
);

if (code == '<table cellspacing="4" style="text-align:right; border: 1px solid;"><tr><th>&nbsp;</th><th>X</th><th>Y</th><th>Z</th></tr><tr><td align="right">1</td><td align="right">30</td><td align="right">31</td><td align="right">32</td></tr><tr><td align="right">2</td><td align="right">33</td><td align="right">34</td><td align="right">35</td></tr><tr><td align="right">3</td><td align="right">36</td><td align="right">37</td><td align="right">38</td></tr><tr><td align="right">4</td><td align="right">39</td><td align="right">40</td><td align="right">41</td></tr><tr><td align="right">5</td><td align="right">42</td><td align="right">43</td><td align="right">44</td></tr></table>') {
    say code;
    say "** Test passed!";
} else {
    die "Error!!!";
}
